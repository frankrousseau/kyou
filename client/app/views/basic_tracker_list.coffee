request = require 'lib/request'
ViewCollection = require 'lib/view_collection'
BasicTrackerCollection = require 'collections/basic_trackers'


# View to display a tracker where data are generated by other Cozy apps
# like the Emails app.
module.exports = class BasicTrackerList extends ViewCollection
    id: 'basic-tracker-list'
    itemView: require 'views/basic_tracker_list_item'
    template: require 'views/templates/basic_tracker_list'
    collection: new BasicTrackerCollection()

    subscriptions:
        'basic-tracker:add': 'onAddBasicTracker'

    constructor: ->
        super

    afterRender: ->
        super


    # Load basic tracker list. Then load data to display for each trackers.
    load: (callback) ->
        @collection.fetch
            success: =>
                @reloadAll ->
                    console.log 'load done'
                    callback?()
            error: =>
                alert 'Cannot load basic trackers'
                callback?()


    # Redraw all charts (useful for resizing or data change).
    redrawAll: ->
        view.drawCharts() for id, view of @views


    # Reload data for each tracker. Perform it one by one.
    reloadAll: (callback) ->
        @$(".tracker .chart").html ''
        @$(".tracker .y-axis").html ''

        trackers = []
        trackers.push view for id, view of @views

        async.eachSeries trackers, (tracker, next) ->
            unless tracker.model.get('metadata').hidden
                tracker.load next
            else
                next()
        , (err) ->
            console.log 'reloadAll done'
            callback() if callback?


    # Hides if the metadata marks it as hidden.
    appendView: (view) ->
        @$collectionEl.append view.el
        if view.model.get('metadata').hidden
            view.$el.addClass 'hidden'


    # When a basic tracker is added, add it to the list and loads its data.
    # Then, it saves the hidden status change so it remembers it for further
    # loading.
    onAddBasicTracker: (slug) ->
        view = @getView slug
        view.$el.removeClass 'hidden'
        view.load()
        data =
            hidden: false
        $('#add-tracker-zone').scrollBottom()
        request.put "basic-trackers/#{slug}", data, (err) ->
            $('#add-tracker-zone').scrollBottom()



    remove: (slug) ->
        view = @getView slug
        view.remove()


    getView: (slug) ->
        tracker = @collection.findWhere slug: slug
        view = @views[tracker.cid]
        return view
